# ГЛАВА 1: ОБРАБОТКА ИЗОБРАЖЕНИЙ<br>раздел 3: Точечные операции

**Точечные операции** используются для изменения значения выбранного пикселя без воздействия на соседние пиксели.
**Они** не изменяют размер, геометрию и локальную структуру изображения.
Такие **операции** могут изменить способ отображения изображения изменяя значения его яркости. 
Входными данными этим **операциям** служат отдельные пиксели.
Взаимно однозначное отображение между исходными (входными) и измененными (выходными) изображениями определяется функцией, определяющей преобразование *T*:

     g(i,j) == T[f(i,j)]

где
- *T* &mdash; преобразования в изображении в серых оттенках, задающее отображение входящего изображения *f* на результат *g*
- *i*, *j* &mdash; индексы строки и столбца для пикселя.

## 11. Гистограммы изображений

Гистограммы используются для многих операций на изображении, в том числе [сжатии](), [сегментации]() и [определении пороговых значений]().
Их анализ дает полезную информацию о контрастности изображения. С их помощью подсчитывают уровни каналов изображения, а при нормализации &mdash; относительную частоту.

`i = `<img align = 'center' style = 'max-width:200px' src = 'img\3\11a1.png'/>

#### визуализация гистограмм:

    ImageHistogram[i]
    ImageHistogram[i, Appearance -> "Stacked"]

`Out[1]:` <img align = 'center' style = 'max-width:200px' src = 'img\3\11a2.png'/>

`Out[2]:` <img align = 'center' style = 'max-width:200px' src = 'img\3\11a3.png'/>

### 11.1 Подсчёт количества значений пикселей

|функция|описание|
|:-|:-|
|[ImageLevels](https://reference.wolfram.com/language/ref/ImageLevels.html?q=ImageLevels)[*image*]|[возвращение](#111-ex1) списка значений пикселей и подсчёт их для каждого канала в *image*|
|[ImageLevels](https://reference.wolfram.com/language/ref/ImageLevels.html?q=ImageLevels)[*image*, *bspec*]|[сортировка](#111-ex2) пикселей в **группы** по спецификации *bspec*|
|[ImageLevels](https://reference.wolfram.com/language/ref/ImageLevels.html?q=ImageLevels)[*image*, *bspec*, *range*]|[сортировка](#111-ex3) пикселей в группы по спецификации *bspec* в заданной области|

##### распределение значений пикселей случайного изображения Laplacian - распределения:

    i = RandomImage[LaplaceDistribution[0, .2], {100, 100}]ImageLevels[i, 50, {-1, 1}] // ListLinePlot

`Out[1]:` <img align = 'center' style = 'max-width:200px' src = 'img\3\111a1.png'/>

`Out[2]:` <img align = 'center' style = 'max-width:200px' src = 'img\3\111a2.png'/>

#### Детализация и опции:

Результат **ImageLevels** &mdash; список уровней для каждого канала

{{*level<sub>1</sub>*, *k<sub>1</sub>*}, {*level<sub>2</sub>*, *k<sub>2</sub>*}, ...}, 

где *k<sub>i</sub>* рассчитывается из значений, удовлетворяющих *level<sub>i</sub>* $\leq$ *k<sub>i</sub>* $\leq$ *level<sub>i+1</sub>*, за исключением последнего.

Для бинарного изображения:

{{0, *n<sub>0</sub>*}, {1, *n<sub>1</sub>*}}

Работает с произвольными ["Grayscale"](#111-ex2) и многоканальными [2D](#111-ex4) и [3D](#111-ex5) изображениями.

##### параметры *bspec*:

|значение *bspec*|описание|
|:-|:-|
|n|n равномерно распределённых **групп**|
|`Automatic`|автоматический выбор сортировки|
|`All`|подсчет всех доступных значений пикселей|
|`"Byte"`|подсчёт на каждом из 256 уровней после преобразования значений данных в "Byte"|

##### параметры *range*:

|значение *range*|описание|
|:-|:-|
|`Automatic`|автоматические диапазоны в зависимости от цветового пространства|
|`All`|все [значения](#111-ex6) для каждого канала|
|{`All`, *max*}|все значения ниже *max*|
|{*min*, `All`}|все значения выше *min*|
|{*min*, *max*}|все [значения](#111-ex7) выше *min*, но ниже *max*|
|{*range<sub>1</sub>*, ...}|различные диапазоны для каждого канала|

##### диапазоны цветовых пространств:

Диапазоны для разных цветовых пространств выбираются таким образом, чтобы включать подпространство цветового пространства **"RGB"**.

|подпространство|диапазон|
|:-|:-|
|"Grayscale"|{0, 1}|
|"RGB"|{0, 1}|
|"CMYK"|{0, 1}|
|"HSB"|{0, 1}|
|"XYZ"|{{0, 0.97}, {0, 1}, {0, 0.83}}|
|"LAB"|{{0, 1}, {-0.8, 0.94}, {-1.13, 0.94}}|
|"LCH"|{{0, 1}, {0, 1.3}, {0, 1}}|
|"LUV"|{{0, 1}, {-0.85, 1.76}, {-1.26, 0.88}}|

##### опции:

|опция|описание|
|:-|:-|
|[Method](https://reference.wolfram.com/language/ref/Method.html?v=14.1)|способ обработки крайних значений|

<div id = '111-method'></div>

###### [опции Method](#111-method-ex):

|опция|по умолчанию|описание|
|:-|:-|:-|
|`"IncludeOutOfRange"`|True|[значения](#111-method-ex) ниже диапазона включаются в первую **группу**, выше диапазона &mdash; в последнюю|
|`"ExcludeOutOfRange"`|False|не считать [значения](#111-method-ex), выходящие за пределы диапазона|

#### Примеры:

<div id = '111-ex1'></div>

##### бинарное изображение:

`ImageLevels[`<img align = 'center' style = 'max-width:200px' src = 'img\3\111c1.png'/>`]`

`Out[]:`

    {{0, 10896}, {1, 4104}}

<div id = '111-ex2'></div>

##### Grayscale изображение:

Подсчёт количества пикселей в каждом из 10 уровней интенсивности

`ImageLevels[`<img align = 'center' style = 'max-width:200px' src = 'img\3\111c2.png'/>`, 10]`

`Out[]:`

    {{0., 9115}, {0.1, 1112}, {0.2, 495}, {0.3, 327}, {0.4, 680}, {0.5, 748}, {0.6, 902}, {0.7, 751}, {0.8, 565}, {0.9, 305}}

<div id = '111-ex4'></div>

##### цветное изображение:

Подсчёт количества пикселей по каждому каналу интенсивности

`ImageLevels[`<img align = 'center' style = 'max-width:200px' src = 'img\3\111c3.png'/>`, 10]`

`Out[]:`

    {{{0., 13393}, {0.1, 283}, {0.2, 404}, {0.3, 585}, {0.4, 736}, {0.5, 658}, {0.6, 515}, {0.7, 230}, {0.8, 127}, {0.9, 19}}, {{0., 2197}, {0.1, 983}, {0.2, 1669}, {0.3, 2991}, {0.4, 4621}, {0.5, 4210}, {0.6, 256}, {0.7, 23}, {0.8, 0}, {0.9, 0}}, {{0., 0}, {0.1, 1}, {0.2, 12}, {0.3, 14}, {0.4, 25}, {0.5, 52}, {0.6, 385}, {0.7, 1499}, {0.8, 2780}, {0.9, 12182}}}

<div id = '111-ex5'></div>

##### 3D изображение:

`ImageLevels[`<img align = 'center' style = 'max-width:200px' src = 'img\3\111c4.png'/>`, 10]`

`Out[]:`

    {{0., 57828}, {0.1, 13779}, {0.2, 15100}, {0.3, 10666}, {0.4, 6822}, {0.5, 3771}, {0.6, 1490}, {0.7, 270}, {0.8, 28}, {0.9, 6}}

##### Byte изображение

Уровни изображения в области [0, 255]  

`ImageLevels[`<img align = 'center' style = 'max-width:200px' src = 'img\3\111c5.png'/>`, "Byte"] // Short`

`Out[]:`

    {{0, 0}, {1, 0}, {2, 0}, {3, 156}, {4, 1791}, {5, 2704}, {6, 1533}, {7, 546}, <<240>>, {248, 0}, {249, 0}, {250, 0}, {251, 0}, {252, 0}, {253, 0}, {254, 0}, {255, 0}}

<div id = '111-ex3'></div>

##### уровни для случайного изображения, имеющего значения интенсивности вне диапазона от 0 до 1

    i = Image[RandomReal[{-2, 3}, {10, 10}]]

`Out[]:` <img align = 'center' style = 'max-width:200px' src = 'img\3\111b1.png'/>

<div id = '111-ex6'></div>

    ImageLevels[i, 10, All]

`Out[]:`

    {{-1.98961, 9}, {-1.49361, 11}, {-0.997602, 12}, {-0.501597, 10}, {-0.00559111, 8}, {0.490414, 8}, {0.98642, 12}, {148243, 10}, {1.97843, 8}, {2.47444, 12}}

<div id = '111-ex7'></div>

###### явная настройка диапазона данных:

    ImageLevels[i, 10, {-2, 3}]

`Out[]:`

    {{-2., 9}, {-1.5, 11}, {-1., 13}, {-0.5, 9}, {0., 9}, {0.5, 7}, {1., 12}, {1.5, 10}, {2., 8}, {2.5, 12}}

###### подсчет всех уровней в случайном изображении: результат с неравномерным распределением ячеек

    ImageLevels[Image[RandomInteger[{0, 255}, {10, 10}]], All]

`Out[]:`

    {{1., 1}, {4., 1}, {10., 1}, {11., 1}, {13., 1}, {15., 3}, {17., 1}, {18., 1}, {23., 2}, {26., 1}, {28., 1}, {31., 1}, {32., 1}, {34., 1}, {38., 1}, {41., 1}, {52., 2}, {55., 2}, {59., 3}, {64., 1}, {67., 1}, {70., 1}, {79., 1}, {82., 1}, {84., 1}, {85., 1}, {88., 1}, {91., 1}, {98., 1}, {100., 1}, {101., 1}, {102., 1}, {104., 1}, {105., 1}, {106., 1}, {107., 1}, {114., 1}, {122., 1}, {123., 2}, {127., 1}, {128., 1}, {129., 1}, {135., 1}, {136., 1}, {138., 1}, {139., 1}, {142., 1}, {143., 1}, {146., 1}, {150., 1}, {153., 2}, {157., 1}, {159., 1}, {160., 1}, {161., 1}, {166., 2}, {167., 1}, {169., 2}, {172., 1}, {175., 1}, {178., 1}, {180., 1}, {187., 1}, {188., 1}, {189., 1}, {191., 2}, {192., 1}, {193., 1}, {195., 2}, {197., 1}, {198., 1}, {208., 1}, {209., 1}, {213., 1}, {214., 1}, {216., 1}, {217., 1}, {219., 1}б {229., 1}, {231., 2}, {233., 1}, {235., 1}, {251., 1}, {253., 2}б {254., 1}}

<div id = '111-method-ex'></div>

###### [Method](#111-method):

    ImageLevels[Image[{{-0.2, 0.0, 0.2, 0.4, 0.6, 0.8, 1.0, 1.2}}], 5, Method -> "IncludeOutOfRange"]
    ImageLevels[Image[{{-0.2, 0.0, 0.2, 0.4, 0.6, 0.8, 1.0, 1.2}}], 5, Method -> "ExcludeOutOfRange"]

`Out[1]:`

    {{0., 2}, {0.2, 1}, {0.4, 1}, {0.6, 1}, {0.8, 3}}

---

`Out[2]:`

    {{0., 1}, {0.2, 1}, {0.4, 1}, {0.6, 1}, {0.8, 2}}

### 11.2 Нахождение глобального порогового значения интенсивности

|функция|описание|
|:-|:-|
|[FindThreshold](https://reference.wolfram.com/language/ref/FindThreshold.html?q=FindThreshold)[*image*]|[поиск](#порог-в-массиве) глобального порогового значения, которое разбивает значения интенсивности изображения на два интервала|

#### Детализация и опции:

- **FindThreshold** преобразует многоканальные и цветные изображения в [Grayscale](), а затем находит глобальный порог для значений интенсивности.
- На основе найденного значения пиксели разделяются на две группы: значения, которые меньше и которые не меньше порога.
- работает с:
    - произвольными [2D](#112-ex1) и [3D](#112-ex2) изображениями
    - [массивами данных](#порог-в-массиве) любого ранга

##### параметры с опциями по умолчанию:

<div id = '112-method'></div>

|параметр|опция|по умолчанию|описание|
|:-|:-|:-|:-|
|[Method](https://reference.wolfram.com/language/ref/Method.html?q=Method)|{"BlackFraction", *b*}|False|порог при условии что *b* [доля](#указание-сколько-данных-должно-быть-ниже-порогового-значения) всех пикселей &mdash; чёрные|
|[Method](https://reference.wolfram.com/language/ref/Method.html?q=Method)|"Cluster"|True|[максимизация](#указание-сколько-данных-должно-быть-ниже-порогового-значения) кластерной дисперсии (алгоритм Otsu)|
|[Method](https://reference.wolfram.com/language/ref/Method.html?q=Method)|"Entropy"|False|минимизации энтропии гистограммы (метод  Капура)|
|[Method](https://reference.wolfram.com/language/ref/Method.html?q=Method)|"Mean"|False|использование средних значений интенсивностей в качестве порога|
|[Method](https://reference.wolfram.com/language/ref/Method.html?q=Method)|"Median"|False|использование медианных значений интенсивностей в качестве порога|
|[Method](https://reference.wolfram.com/language/ref/Method.html?q=Method)|"MinimumError"|False|[метод](#method-minimumerror) минимальной ошибки Киттлера-Иллингворта: классификация пикселей изображения из двух Гауссовых распределений|

#### Примеры:

##### порог в массиве:

    FindThreshold[{1, 2, 3, 10, 11}]

<div id = '112-ex1'></div>

##### цветное изображение:

`FindThreshold[`<img align = 'center' style = 'max-width:200px' src = 'img\2\83b1.png'/>`]`

`Out[]:`

    0.47451

<div id = '112-ex2'></div>

##### 3D изображение:

`FindThreshold[`<img align = 'center' style = 'max-width:200px' src = 'img\2\101h1.png'/>`]`

`Out[]:`

    0.254902

##### [Method "MinimumError"](#112-method):

###### два Гауссовых распределения:

    r = RandomReal[MixtureDistribution[{3/5, 2/5}, {NormalDistribution[1, 1/2], NormalDistribution[2, 1/6]}], 10^6];
    t = FindThreshold[r, Method -> "MinimumError"];
    Show[Histogram[r, 50], Graphics[{Blue, Line[{{t, 0}, {t, 10^5}}]}]]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\112a1.png'/>

##### указание, сколько данных должно быть ниже порогового значения:

    r = RandomReal[NormalDistribution[1, 1/2], 10^6];
    Table[t = FindThreshold[r, Method -> {"BlackFraction", f}]; Show[Histogram[r, 50], Graphics[{Blue, Line[{{t, 0}, {t, 10^5}}]}], PlotLabel -> f], {f, {.2, .5, .8}}]

`Out[]:{`<img align = 'center' style = 'max-width:200px' src = 'img\3\112a2.png'/>`, `<img align = 'center' style = 'max-width:200px' src = 'img\3\112a3.png'/>`, `<img align = 'center' style = 'max-width:200px' src = 'img\3\112a4.png'/>`}` 

#### Применение:

##### нахождение порога для сегментации изображения:

`i = `<img align = 'center' style = 'max-width:200px' src = 'img\3\112b1.png'/>`;`

    Binarize[i, FindThreshold[i]]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\112b2.png'/>`

##### нахождение порога для выделения вертикального размера букв в тексте

`i = `<img align = 'center' style = 'max-width:200px' src = 'img\3\112c1.png'/>`;`
    
    l = FindThreshold[(Differences /@ ComponentMeasurements[i, "BoundingBox"][[All, 2]])[[All, 1, 2]]];
    s = SelectComponents[i, "BoundingBox", Abs[#[[1]] - #[[2]]][[2]] > l &];
    ColorCombine[{i, ImageSubtract[i, s], i}]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\112c2.png'/>

### 11.3 Замена значений яркости, связанная с пороговым значением

|функция|описание|
|:-|:-|
|[Threshold](https://reference.wolfram.com/language/ref/Threshold.html?q=Threshold)[*data*]|[замена](#113-ex1) порогового значения ([FindThreshold](#112-нахождение-глобального-порогового-значения-интенсивности)) данных *data*  путем изменения  значений близких к нулю на ноль|
|[Threshold](https://reference.wolfram.com/language/ref/Threshold.html?q=Threshold)[*data*, *tspec*]|замена порогового значения данных *data* согласно параметру *tspec*|
|[Threshold](https://reference.wolfram.com/language/ref/Threshold.html?q=Threshold)[*data*, {*tfun*, *pars*}]|параметр *tspec* может иметь [форму](#113-ex2) {*tfun*, *pars*}|
|[Threshold](https://reference.wolfram.com/language/ref/Threshold.html?q=Threshold)[*image*, ...]|замена порогового значения изображения *image*|
|[Threshold](https://reference.wolfram.com/language/ref/Threshold.html?q=Threshold)[*sound*, ...]|замена порогового значения звукового объекта *sound*|

<div id = '113-ex1'></div>

    FourierDST[N[{1, 0, 1, 0, 1}]]
    Threshold[%]

`Out[1]:`

    {0.723607, 8.32667*10^-17, 0.276393, 0., 1.34164}

`Out[2]:`

    {0.723607, 0., 0.276393, 0., 1.34164}

#### Детализация и опции:

- **Threshold** заменяет значения яркостей пикселя в зависимости от порогового значения.

- работает с:

    - произвольными [2D](#112-ex1) и [3D](#112-ex2) изображениями
    
    - [массивами данных](#113-ex1) любого ранга
---

##### по умолчанию:

    Threshold[data] == Threshold[data, {"Hard", 10^-10}]

<div id = '113-ex2'></div>

##### Пороговое значение δ это:
- или положительное число;
- или функция  *tfun*, результатом которой  является положительное число.

##### параметры *tfun*: функции обработки пороговых значений

Используемые параметры:
- *r* - положительное вещественное число, 
- *p* - вещественное число между 0 и 1,
- *n* - положительное машинное целое число.

###### определения пороговых функций:

|опция|пример|формула|
|:-|:-|:-|
|`{"Hard", δ}`|[пример](#largestvalues)|$\begin{cases} 0 & \|x\| \leq \delta  \\ x & \|x\| >\delta \end{cases}$ |
|`{"Soft", δ}`|[пример](#hard)|$\begin{cases} 0 & \|x\| \leq \delta  \\ \text{sgn}(x) (\|x\| -\delta ) & \|x\| >\delta \end{cases}$|
|`{"Firm", δ}`|[пример](#firm)|$\begin{cases} \begin{array}{ll}  0 & \|x\| \leq \delta -\delta  p r \\  \frac{1}{\delta  r}\text{sgn}(x) (\delta +\delta  r-\delta  p\text{  }r) (\|x\| -\delta +\delta  p r) & \delta -\delta  p r<\|x\| \leq \delta +\delta  (-p) r+\delta  r \\  x & \|x\| >\delta +\delta  (-p) r+\delta  r \\ \end{array}  \\ \end{cases}$|
|`{"PiecewiseGarrote", δ}`|[пример](#piecewisegarrote)|$\begin{cases}  0 & \|x\| \leq \delta  \\  x-\frac{\delta ^2}{x} & \|x\| >\delta  \end{cases}$|
|`{"SmoothGarrote", δ}`|[пример](#smoothgarrote)|$\frac{x^{2 n+1}}{x^{2 n}+\delta ^{2 n}}$|
|`{"Hyperbola", δ}`|[пример]()|$\begin{cases}  0 & \|x\| \leq \delta  \\  \text{sgn}(x) \sqrt{x^2-\delta ^2} & \|x\| >\delta  \end{cases}$|
|`{"LargestValues", k}`|[пример]()|остаётся *k*  точек с самыми большими значениями|

##### примеры пороговых функций *tfun*:

###### LargestValues:

    Threshold[Range[10], {"LargestValues", 5}]

`Out[]:`

    {0, 0, 0, 0, 0, 6, 7, 8, 9, 10}

###### Hard:

    Threshold[data, "Hard"] == Chop[data]

---

    hardThreshold[x_, δ_] := Chop[x, δ]
    Plot[hardThreshold[x, 1], {x, -3, 3}]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113a1.png'/>

###### Soft:

`Threshold[data, "Soft"]` выполняет операцию сжатия: значения данных, ниже определенного δ, устанавливаются равными 0; значения выше &mdash; "сжимаются" на δ.

    softThreshold[x_, δ_] :=  Sign[x] Max[0, (Abs[x] - δ)]
    Plot[softThreshold[x, 1], {x, -3, 3}]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113a2.png'/>

###### Firm:

`Threshold[data, "Firm"]` &mdash; композиция опций ["Hard"](#hard) и  ["Soft"](#soft).
Дисперсия меньше, чем с опцией  ["Hard"](#hard)

    firmThreshold[x_, a_, b_] := Piecewise[{{0, Abs[x] <= a}, {Sign[x] (b (Abs[x] - a)/(b - a)), a < Abs[x] <= b}, {x, Abs[x] > b}}]
    Plot[firmThreshold[x, 1, 2], {x, -3, 3}]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113a3.png'/>

При  b &rarr; &infin;  опция **"Firm"** совпадает с  ["Soft"](#soft).

    Plot[firmThreshold[x, 1, 10], {x, -3, 3}]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113a4.png'/>

При  b &rarr; a  опция **"Firm"** совпадает с  ["Hard"](#hard).

    Plot[firmThreshold[x, 1, 1], {x, -3, 3}, Exclusions -> None]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113a5.png'/>

###### PiecewiseGarrote:

Опция "PiecewiseGarrote" совпадает с  ["Hard"](#hard)  но его преимущество &mdash; наличие одного параметра  δ.

    piecewiseGarrote[x_, δ_] :=  Piecewise[{{x - δ^2/x, Abs[x] > δ}}]
    Plot[piecewiseGarrote[x, 1], {x, -3, 3}]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113a6.png'/>

###### SmoothGarrote:

При n &rarr; &infin;  опция "SmoothGarrote" совпадает с  ["Hard"](#hard).

    smoothGarrote[x_, δ_, n_] := x^(2 n + 1)/(δ^(2 n) + x^(2 n))
    Plot[{smoothGarrote[x, 1, 3], smoothGarrote[x, 1, 10], smoothGarrote[x, 1, 60]}, {x, -3, 3}]

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113a7.png'/>

##### параметры с опциями по умолчанию:

|параметр|опция по умолчанию|описание|
|:-|:-|:-|
|[Method](https://reference.wolfram.com/language/ref/Method.html?q=Method)|"Cluster"|способ определения порога|

##### возможные опции Method:

В **Threshold** параметр [Method](https://reference.wolfram.com/language/ref/Method.html?q=Method) устанавливает способ определения порога.

опция|по умолчанию|описание|
|:-|:-|:-|
|{"BlackFraction", *b*}|False|порог при условии что *b* [доля](#указание-сколько-данных-должно-быть-ниже-порогового-значения) всех пикселей &mdash; чёрные|
|"Cluster"|True|[максимизация](#указание-сколько-данных-должно-быть-ниже-порогового-значения) кластерной дисперсии (алгоритм Otsu)|
|"Entropy"|False|минимизации энтропии гистограммы (метод  Капура)|
|"Mean"|False|использование средних значений интенсивностей в качестве порога|
|"Median"|False|использование медианных значений интенсивностей в качестве порога|
|"MinimumError"|False|[метод](#method-minimumerror) минимальной ошибки Киттлера-Иллингворта: классификация пикселей изображения из двух Гауссовых распределений|

#### Примеры:

##### фото паука:

`Threshold[`<img align = 'center' style = 'max-width:200px' src = 'img\3\113b1.png'/>`, {"Hard", "Cluster"}]`

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113b2.png'/>

##### фото улиток:

`Threshold[`<img align = 'center' style = 'max-width:200px' src = 'img\3\113c1.png'/>`, {"Soft", "Cluster"}]`

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113c2.png'/>

##### гаструла лягушки:

`Threshold[`<img align = 'center' style = 'max-width:200px' src = 'img\3\113d1.png'/>`, {"Hard", "Cluster"}]`

`Out[]:`<img align = 'center' style = 'max-width:200px' src = 'img\3\113d2.png'/>

